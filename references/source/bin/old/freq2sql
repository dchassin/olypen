#!/bin/gawk -f

BEGIN {
	printf "# output by freq2sql\n# started %s\n", strftime();
	first = 1;
}

{
	n = split($0,data,/; */);
	done = 0;
	if (n>=2)
	{
		n1 = split(data[1],term1,"=");
		n2 = split(data[2],term2,"=");
		if (n1==2 && n2==2)
		{
			now = strftime("%Y-%m-%d %H:%M:%S");
			if (term1[1]=="freq" && term2[1]=="time") 
			{
				f = term1[2]; t = term2[2];
				done = 1;
			}
			else if (term1[1]=="time" && term2[1]=="freq")
			{
				t = term1[2]; f = term2[2];
				done = 1;
			}
		}
	}
	if (done==0)
	{
		printf "# %s\n", $0;
	}
	else
	{
		if (first==1)
		{
			printf "INSERT IGNORE INTO gridfreq (posttime, readtime, freq) VALUES\n";
			first = 0;
		}
		else
			printf ",\n";
		printf "('%s',%.6f,%.3f)", now, t, f;
	}
}

END {
	if (first==0)
		printf ";\n";
	printf "# ended %s\n", strftime();
}
